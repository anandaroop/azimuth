- map_title "NYC 100" do
  %p Fetches 10 pages / 100 shows from the API to give a fuller picture of the # of shows in an area

#map{ style: 'position: absolute; left: 0; top: 0; width: 100%; height: 100%; z-index: -1' }

:coffeescript
  class Azimuth.NYC100Map
    constructor: (options = {}) ->
      { selector, mapOptions } = options
      @$el = $(selector)
      @initialize(mapOptions)
      @fetchData()

    initialize: (options) ->
      @map = L.map(@$el.get(0), options)
      @map.addLayer L.tileLayer.provider('Esri.WorldGrayCanvas')

    fetchData: ->
      $.getJSON location + '.json'
        .done (data) =>
          @updateMap data
        .fail ->
          alert 'Could not fetch the data!'

    updateMap: (data) ->
      allCoordinates = [] # keep a list so we can recenter the map
      _.each data, (show) =>
        if show.location?
          { id, name, location : { coordinates } } = show
          showLink = '<a href="https://www.artsy.net/show/' + id + '">' + name + '</a>'
          @map.addControl L.marker(coordinates, title: name, alt: name).bindTooltip(showLink)
          allCoordinates.push(coordinates)
      bounds = L.latLngBounds(allCoordinates) 
      @map.fitBounds(bounds)


  $ ->
    window.azmap = new Azimuth.NYC100Map
      selector: '#map'
      mapOptions:
        center: [40.71, -74.01]
        zoom: 10
