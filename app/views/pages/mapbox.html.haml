- map_title "Mapbox Map" do
  %p Employs a custom design for a vector tile-based map, created in Mapbox Studio. (Because the map design is optimized for city-scale mapping, the map viewport is bound to NYC again.)
  %p New helpers for Mapbox API and Artsy colors
  %p (If the map tiles aren't loading it's because you need a <code>mapbox_access_token</code> in your .env file)

#map{ style: 'position: absolute; left: 0; top: 0; width: 100%; height: 100%; z-index: -1' }

:coffeescript
  class Azimuth.MapboxMap
    constructor: (options = {}) ->
      { selector, mapOptions } = options
      @$el = $(selector)
      @initialize(mapOptions)
      @fetchData()

    initialize: (options) ->
      @map = L.map(@$el.get(0), options)
      @map.addLayer L.tileLayer Azimuth.Mapbox.urlFor(options.mapboxStyleId, '#{@mapbox_token}')
      @center = options.center
      @markers = L.layerGroup().addTo(@map)

    fetchData: ->
      $.getJSON location + '.json', latlng: @center.lat + ',' + @center.lng
        .done (data) =>
          @updateMap data
        .fail ->
          alert 'Could not fetch the data!'

    updateMap: (data) ->
      _.each data, (show) =>
        if show.location?
          { id, name, location : { coordinates } } = show
          showLink = '<a href="https://www.artsy.net/show/' + id + '">' + name + '</a>'
          marker = L.circleMarker(coordinates,
            radius: 8,
            # stroke
            stroke: true,
            color: '#ffffff',
            weight: 1.5,
            opacity: 1,
            # fill
            fill: true,
            fillColor: Azimuth.Colors.purpleRegular,
            fillOpacity: 0.75
          ).bindTooltip(showLink)
          @markers.addLayer(marker)

  $ ->
    window.azmap = new Azimuth.MapboxMap
      selector: '#map'
      mapOptions:
        # mapboxStyleId: 'cirwtpzrd001sg6m8uzt45aw9' # v1
        # mapboxStyleId: 'cirww2yfg001zg9ku20gk3w9u' # v2 chill on the neighborhood names
        mapboxStyleId: 'cirwwo4ii001ug6m8zf4co2d5' # v3 chill on the roads at smaller scales
        center: Azimuth.HQ
        zoom: 13
        minZoom: 11
        maxBounds: [
          [40.50910079,-74.30877685]
          [40.91299367,-73.74710083]
        ]
